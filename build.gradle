import org.asciidoctor.gradle.AsciidoctorTask
import javax.inject.Inject

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16"
  }
}
plugins {
  id "org.asciidoctor.convert" version "1.6.0"
}

ext {
  currentDate = new Date().format("d. MMM yyyy")
  project.version = "0.4.2 DRAFT ($currentDate)"
  curriculumBaseName = "template-curriculum"
  addSuffixToCurriculum = { suffix ->
    for (extension in ["html", "pdf"]) {
      File source = new File("${buildDir}/${curriculumBaseName}.${extension}")
      File target = new File("${buildDir}/${curriculumBaseName}${suffix}.${extension}")

      source.renameTo(target)
    }
  }
}

class RenderCurriculumTask extends AsciidoctorTask {
  final String currentDate
  final String language
  final boolean withRemarks

  @Inject
  RenderCurriculumTask(String currentDate, String language, boolean withRemarks) {
    sourceDir = new File("./docs/")
    sources {
      include "index.adoc"
      include "template-curriculum.adoc"
    }
    outputDir = new File("./build/")
    separateOutputDirs = false
    backends 'pdf', 'html5'
    attributes = [
      'icons'            : 'font',
      'document-version' : "${project.version}",
      'currentDate'      : currentDate,
      'language'         : language,
      'withRemarks'      : withRemarks,
      'debug_adoc'       : false,
      'pdf-stylesdir'    : '../pdf-theme/themes',
      'pdf-style'        : 'isaqb'
    ]
  }
}

task buildDocs {
	group 'Documentation'
	description 'Grouping task for generating all languages in several formats'
}

task renderNoRemarksDE(type: RenderCurriculumTask, constructorArgs: [currentDate, "DE", false]) {
  doLast {
    addSuffixToCurriculum("_de")
  }
}

task renderNoRemarksEN(type: RenderCurriculumTask, constructorArgs: [currentDate, "EN", false]) {
  doLast {
    addSuffixToCurriculum("_en")
  }
}

task renderWithRemarksDE_EN(type: RenderCurriculumTask, constructorArgs: [currentDate, "DE;EN", true]) {
  doLast {
    addSuffixToCurriculum("_remarks_de_en")
  }
}

buildDocs.dependsOn renderNoRemarksDE
buildDocs.dependsOn renderNoRemarksEN
buildDocs.dependsOn renderWithRemarksDE_EN

